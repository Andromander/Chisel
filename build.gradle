buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }

}

apply plugin: 'forge'
apply plugin: 'maven'
apply plugin: 'curseforge'
version = "2.2.2"
group = "com.cricketcraft.chisel" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Chisel 2"

minecraft {
    version = "1.7.10-10.13.2.1277"
    runDir = "run"

    replaceIn "Chisel.java"
    replace "@MOD_VERSION@", project.version
}

repositories {
	maven {
		name 'Chicken Bones Repo'
		url "http://chickenbones.net/maven/"
	}

    maven {
		name 'Prof Mobius Repo'
		url "http://mobiusstrip.eu/maven"
	}
}

dependencies {
    compile "mcp.mobius.waila:Waila:1.5.3_1.7.10:dev"
	compile "codechicken:CodeChickenLib:1.7.10-1.1.1.95:dev"
	compile "codechicken:CodeChickenCore:1.7.10-1.0.2.11:dev"
	compile "codechicken:NotEnoughItems:1.7.10-1.0.2.26:dev"
    compile "codechicken:ForgeMultipart:1.7.10-1.1.0.297:dev"
}

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                // replace version and mcversion
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

// Create deobf dev jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

// Create source attachment for dev jar
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourcesJar
}

println(getChangelogText())
String getChangelogText() {
    def changelogFile = new File('changelog')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            str += "$it" + (lineCount == 0 ? '\n\n' : '\n')
            return
        } else {
            done = true
        }
    }
    return str
}

curse {
    dependsOn 'reobf'

    onlyIf {
        return project.hasProperty('curseforge_key') && System.getenv("CURSE") != null && System.getenv("CURSE").equalsIgnoreCase("true")
    }

    if (project.hasProperty('curseforge_key')) apiKey = project.curseforge_key
    projectId = project.curse_projectId
    changelog = System.getenv("CHANGELOG").equals("none") ? getChangelogText() : System.getenv("CHANGELOG")
    releaseType = project.hasProperty('mod_appendix') ? "${mod_appendix}".toString() : 'release'
}
